plugins {
  id 'java'
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '1.8.22'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
  id 'edu.sc.seis.launch4j' version '3.0.4'
}

group 'com.motompro.tcpconfig'
version '2.0'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.9.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'com.motompro.tcpconfig.app'
  mainClass = 'com.motompro.tcpconfig.app.TCPConfigApp'
}

kotlin {
  jvmToolchain( 17 )
}

javafx {
  version = '17.0.6'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation("org.yaml:snakeyaml:2.2")

  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
  useJUnitPlatform()
}

launch4j {
  outfile = "TCPConfig.exe"
  bundledJrePath = 'jre'
  jreMinVersion = '17'
  mainClassName = 'com.motompro.tcpconfig.app.TCPConfigAppKt'
  productName = 'TCPConfig'
  icon = "${projectDir}/icon/app-icon.ico"
  manifest = "${projectDir}/TCPConfig.manifest"
  jarTask = project.tasks.jar
}

tasks.register("packageExecutableDistribution", Zip) {
  archiveFileName = "TCPConfig.zip"
  destinationDirectory = file("$buildDir/distExecutable")

  from "$buildDir/launch4j"
}

tasks.register("addJreAndUpdaterToDistributable", Copy) {
  from zipTree("jre/win_jre_17.zip")
  destinationDir = file("$buildDir/launch4j")
}

tasks.named("packageExecutableDistribution").get().dependsOn("createExe")
tasks.named("packageExecutableDistribution").get().dependsOn("addJreAndUpdaterToDistributable")
